# File I/O (lib/io)

The `lib/io` module provides functions for working with files and console.

```rust
import "lib/io" (*)
```

## Console Input

### readLine

```rust
readLine() -> Option<String>
```

Reads a line from stdin. Returns `None` at EOF.

```rust
import "lib/io" (readLine)

print("Enter name:")
match readLine() {
    Some(name) -> print("Hello, ${name}!")
    None -> print("EOF")
}
```

#### Reading Multiple Lines

```rust
import "lib/io" (readLine)

fun readAllLines() -> List<String> {
    lines = []
    while true {
        match readLine() {
            Some(line) -> lines = lines ++ [line]
            None -> break
        }
    }
    lines
}
```

### readAll

```rust
readAll() -> String
```

Reads **all** of stdin into a single string. Useful for piping data into Funxy scripts.

```rust
import "lib/io" (readAll)

content = readAll()
print("Read " ++ show(len(content)) ++ " characters")
```

Typical use in bash pipelines:

```bash
cat data.txt | funxy process.lang
```

### readAllBytes

```rust
readAllBytes() -> Bytes
```

Reads **all** of stdin into a `Bytes` value. Useful for processing binary data (images, archives, etc.) from pipes.

```rust
import "lib/io" (readAllBytes)

data = readAllBytes()
print("Read " ++ show(len(data)) ++ " bytes")
```

## File Operations

### fileRead

```rust
fileRead(path: String) -> Result<String, String>
```

Reads entire file into a string.

```rust
import "lib/io" (fileRead)

match fileRead("data.txt") {
    Ok(content) -> print(content)
	Fail(err) -> print("Error: ${err}")
}
```

### fileReadBytes

```rust
fileReadBytes(path: String) -> Result<String, Bytes>
```

Reads entire file into a Bytes object. Useful for binary files (images, archives).

```rust
import "lib/io" (fileReadBytes)

match fileReadBytes("image.png") {
    Ok(data) -> print("Read ${len(data)} bytes")
    Fail(err) -> print("Error: ${err}")
}
```

### fileReadBytesAt

```rust
fileReadBytesAt(path: String, offset: Int, length: Int) -> Result<String, Bytes>
```

Reads part of file from specified position as Bytes.

```rust
import "lib/io" (fileReadBytesAt)

match fileReadBytesAt("image.png", 0, 8) {
    Ok(header) -> print("Read header: ${header}")
    Fail(err) -> print("Error: ${err}")
}
```

### fileReadAt

```rust
fileReadAt(path: String, offset: Int, length: Int) -> Result<String, String>
```

Reads part of file from specified position.

```rust
import "lib/io" (fileReadAt)

// Read 10 bytes starting from position 100
match fileReadAt("big_file.txt", 100, 10) {
    Ok(chunk) -> print(chunk)
    Fail(err) -> print("Error: ${err}")
}
```

### fileWrite

```rust
fileWrite(path: String, content: String | Bytes) -> Result<String, Int>
```

Writes string or bytes to file (overwrites if exists). Returns number of bytes written.

```rust
import "lib/io" (fileWrite)

match fileWrite("output.txt", "Hello, World!") {
    Ok(bytes) -> print("Wrote ${bytes} bytes")
    Fail(err) -> print("Error: ${err}")
}
```

### fileAppend

```rust
fileAppend(path: String, content: String | Bytes) -> Result<String, Int>
```

Appends string or bytes to end of file.

```rust
import "lib/io" (fileAppend)

fileAppend("log.txt", "New entry\n")
```

### fileExists

```rust
fileExists(path: String) -> Bool
```

Checks if file exists.

```rust
import "lib/io" (fileExists, fileRead)

if fileExists("config.txt") {
    config = fileRead("config.txt")
    // ...
} else {
    print("Config not found")
}
```

### fileSize

```rust
fileSize(path: String) -> Result<String, Int>
```

Returns file size in bytes.

```rust
import "lib/io" (fileSize)

match fileSize("data.bin") {
    Ok(size) -> print("Size: ${size} bytes")
    Fail(err) -> print("Error: ${err}")
}
```

### fileDelete

```rust
fileDelete(path: String) -> Result<String, Nil>
```

Deletes file.

```rust
import "lib/io" (fileDelete)

match fileDelete("temp.txt") {
    Ok(_) -> print("File deleted")
    Fail(err) -> print("Error: ${err}")
}
```

## Directory Operations

### dirCreate

```rust
dirCreate(path: String) -> Result<String, Nil>
```

Creates directory. Parent directory must exist.

```rust
import "lib/io" (dirCreate)

match dirCreate("data") {
    Ok(_) -> print("Directory created")
    Fail(err) -> print("Error: ${err}")
}
```

### dirCreateAll

```rust
dirCreateAll(path: String) -> Result<String, Nil>
```

Creates directory and all parent directories.

```rust
import "lib/io" (dirCreateAll)

// Will create data/, data/cache/, data/cache/temp/
dirCreateAll("data/cache/temp")
```

### dirRemove

```rust
dirRemove(path: String) -> Result<String, Nil>
```

Removes empty directory.

```rust
import "lib/io" (dirRemove)

match dirRemove("empty_dir") {
    Ok(_) -> print("Directory removed")
    Fail(err) -> print("Error: ${err}")  // not empty?
}
```

### dirRemoveAll

```rust
dirRemoveAll(path: String) -> Result<String, Nil>
```

Recursively removes directory and all its contents.

```rust
import "lib/io" (dirRemoveAll)

// Will remove temp/ with all contents
dirRemoveAll("temp")
```

### dirList

```rust
dirList(path: String) -> Result<String, List<String>>
```

Returns list of files and subdirectories.

```rust
import "lib/io" (dirList)

match dirList(".") {
    Ok(items) -> {
        for item in items {
            print(item)
        }
    }
    Fail(err) -> print("Error: ${err}")
}
```

### dirExists

```rust
dirExists(path: String) -> Bool
```

Checks if directory exists.

```rust
import "lib/io" (dirExists, dirCreate)

if !dirExists("cache") {
    dirCreate("cache")
}
```

### isDir

```rust
isDir(path: String) -> Bool
```

Checks if path is a directory.

```rust
import "lib/io" (isDir)

if isDir("data") {
    print("This is a directory")
}
```

### isFile

```rust
isFile(path: String) -> Bool
```

Checks if path is a regular file.

```rust
import "lib/io" (isFile, isDir)

path = "data.txt"
if isFile(path) {
    print("This is a file")
} else if isDir(path) {
    print("This is a directory")
} else {
    print("Does not exist")
}
```

## ? Operator for Simplification

The `?` operator automatically unwraps `Result` and propagates errors:

```rust
import "lib/io" (fileRead, fileWrite)

fun copyFile(src: String, dst: String) -> Result<String, Int> {
    content = fileRead(src)?       // Fail -> immediate return
    bytes = fileWrite(dst, content)?
    Ok(bytes)
}

match copyFile("a.txt", "b.txt") {
    Ok(bytes) -> print("Copied ${bytes} bytes")
    Fail(err) -> print("Error: ${err}")
}
```

## Practical Examples

### Reading File Line by Line

```rust
import "lib/io" (fileRead)
import "lib/string" (stringLines)

fun readLines(path: String) -> Result<String, List<String>> {
    match fileRead(path) {
        Ok(content) -> Ok(stringLines(content))
        Fail(err) -> Fail(err)
    }
}
```

### Simple Logger

```rust
import "lib/io" (fileAppend)
import "lib/time" (timeNow)

fun log(message: String) -> Nil {
    timestamp = timeNow()
    line = "[${show(timestamp)}] ${message}\n"
    fileAppend("app.log", line)
    Nil
}

log("Application started")
log("Processing data...")
```

### Reading Configuration

```rust
import "lib/io" (fileRead, fileExists)
import "lib/string" (stringLines, stringSplit, stringTrim)
import "lib/list" (length, filter, map, foldl)
import "lib/map" (mapPut)

fun loadConfig(path: String) -> Result<String, Map<String, String>> {
    if !fileExists(path) {
        Fail("Config file not found")
    } else {
        match fileRead(path) {
            Fail(e) -> Fail(e)
            Ok(content) -> {
                lines = stringLines(content)
                    |> filter(fun(line) -> length(stringTrim(line)) > 0)
                    |> filter(fun(line) -> stringTrim(line)[0] != '#')

                config = foldl(fun(acc, line) -> {
                    parts = stringSplit(stringTrim(line), "=")
                    if length(parts) == 2 {
                        key = stringTrim(parts[0])
                        value = stringTrim(parts[1])
                        mapPut(acc, key, value)
                    } else { acc }
                }, %{}, lines)

                Ok(config)
            }
        }
    }
}
```

### Recursive Directory Traversal

```rust
import "lib/io" (dirList, isDir, isFile)
import "lib/path" (pathJoin)
import "lib/string" (stringEndsWith)

fun walkDir(path: String, callback: (String) -> Nil) -> Nil {
    match dirList(path) {
        Ok(items) -> {
            for item in items {
                fullPath = pathJoin([path, item])
                if isFile(fullPath) {
                    callback(fullPath)
                } else if isDir(fullPath) {
                    walkDir(fullPath, callback)
                }
            }
        }
        Fail(_) -> Nil
    }
}

// Find all .txt files
walkDir(".", fun(path) -> {
    if stringEndsWith(path, ".txt") {
        print(path)
    }
})
```

### Creating Temporary Directory

```rust
import "lib/io" (dirCreateAll, dirRemoveAll, fileWrite)
import "lib/path" (pathTemp, pathJoin)

fun withTempDir(callback: (String) -> Nil) -> Nil {
    tempPath = pathTemp()
    match dirCreateAll(tempPath) {
        Ok(_) -> {
            callback(tempPath)
            match dirRemoveAll(tempPath) {
                Ok(_) -> Nil
                Fail(_) -> Nil
            }
        }
        Fail(_) -> Nil
    }
}

// Usage
withTempDir(fun(dir) -> {
    fileWrite(pathJoin([dir, "data.txt"]), "temporary data")
    // ... work with temporary files
    Nil
})
```

## Error Handling

All file operations return `Result<String, T>`:
- `Ok(value)` — success
- `Fail(errorMessage)` — error with description

Common errors:
- "file not found" — file doesn't exist
- "permission denied" — no access rights
- "is a directory" — path points to a directory

## Summary

| Function | Type | Description |
|---------|-----|----------|
| `readLine` | `() -> Option<String>` | Read line from stdin |
| `readAll` | `() -> String` | Read all of stdin as string |
| `readAllBytes` | `() -> Bytes` | Read all of stdin as bytes |
| `fileRead` | `(String) -> Result<String, String>` | Read entire file |
| `fileReadBytes` | `(String) -> Result<String, Bytes>` | Read file as bytes |
| `fileReadBytesAt` | `(String, Int, Int) -> Result<String, Bytes>` | Read part of file as bytes |
| `fileReadAt` | `(String, Int, Int) -> Result<String, String>` | Read part of file |
| `fileWrite` | `(String, String \| Bytes) -> Result<String, Int>` | Write file |
| `fileAppend` | `(String, String \| Bytes) -> Result<String, Int>` | Append to file |
| `fileExists` | `(String) -> Bool` | Check file existence |
| `fileSize` | `(String) -> Result<String, Int>` | File size |
| `fileDelete` | `(String) -> Result<String, Nil>` | Delete file |
| `dirCreate` | `(String) -> Result<String, Nil>` | Create directory |
| `dirCreateAll` | `(String) -> Result<String, Nil>` | Create directory recursively |
| `dirRemove` | `(String) -> Result<String, Nil>` | Remove empty directory |
| `dirRemoveAll` | `(String) -> Result<String, Nil>` | Remove directory recursively |
| `dirList` | `(String) -> Result<String, List<String>>` | Directory contents |
| `dirExists` | `(String) -> Bool` | Check directory existence |
| `isDir` | `(String) -> Bool` | Is directory? |
| `isFile` | `(String) -> Bool` | Is file? |
