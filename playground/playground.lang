package playground
import "lib/http" (httpServe)
import "lib/sys" (sysExec, sysArgs)
import "lib/io" (fileWrite, fileDelete, fileRead)
import "lib/json" (jsonEncode, jsonDecode)
import "lib/uuid" (uuidNew, uuidToString)
import "lib/path" (pathJoin, pathTemp, pathDir)
import "lib/list" (head)
import "lib/string" (stringIndexOf)

// Get directory of this script
scriptDir = match sysArgs() {
    [path, ..._] -> pathDir(path)
    _ -> "."
}

fun runCode(code: String) -> { output: String, error: String } {
    tmpFile = pathJoin([pathTemp(), "funxy_" ++ uuidToString(uuidNew()) ++ ".lang"])

    match fileWrite(tmpFile, code) {
        Fail(e) -> { output: "", error: "Failed to write: " ++ e }
        Ok(_) -> {
            // Try ./funxy first, then fallback to funxy in PATH
            result = sysExec("./funxy", [tmpFile])

            finalResult = if result.code == -1 {
                 // If ./funxy failed to execute (likely not found), try 'funxy' from PATH
                 match stringIndexOf(result.stderr, "no such file") {
                     Some(_) -> sysExec("funxy", [tmpFile])
                     _ -> result
                 }
            } else {
                 result
            }

            fileDelete(tmpFile)

            if finalResult.code == 0 {
                { output: finalResult.stdout, error: "" }
            } else {
                { output: finalResult.stdout, error: finalResult.stderr }
            }
        }
    }
}

fun handler(req) {
    match (req.method, req.path) {
        ("GET", "/") -> {
            htmlPath = pathJoin([scriptDir, "playground.html"])
            match fileRead(htmlPath) {
                Ok(html) -> { status: 200, body: html, headers: [("Content-Type", "text/html")] }
                Fail(e) -> { status: 500, body: "Failed to load: " ++ htmlPath ++ " - " ++ e, headers: [] }
            }
        }
        ("POST", "/run") -> {
            match jsonDecode(req.body) {
                Ok(data) -> {
                    result = runCode(data.code)
                    { status: 200, body: jsonEncode(result), headers: [("Content-Type", "application/json")] }
                }
                Fail(e) -> { status: 400, body: jsonEncode({ error: e }), headers: [] }
            }
        }
        _ -> { status: 404, body: "Not Found", headers: [] }
    }
}

print("Playground: http://localhost:8080")
httpServe(8080, handler)
