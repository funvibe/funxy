package web

import "lib/time" (clockMs)

// --- Middleware ---

fun logger(next) {
    fun(ctx: Context) {
        start = clockMs()
        result = next(ctx)
        duration = clockMs() - start
        req = ctx.req
        match result {
            Ok(res) -> {
                respObj = autoResponse(res)
                msg = format("%s %s -> %d (%d ms)", req.method, req.path, respObj.status, duration)
                print(msg)
            }
            Fail(err) -> {
                msg = format("%s %s -> ERROR (%d ms)", req.method, req.path, duration)
                print(msg)
            }
        }
        result
    }
}

fun recover(next) {
    fun(ctx: Context) {
        match next(ctx) {
            Ok(res) -> Ok(res)
            Fail(err) -> {
                resp: Response = { status: 500, headers: [("Content-Type", "text/plain")], body: BodyString("Internal Server Error") }
                Ok(resp)
            }
        }
    }
}

fun cors(next) {
    fun(ctx: Context) {
        if ctx.req.method == "OPTIONS" {
             resp: Response = {
                status: 204,
                headers: [
                    ("Access-Control-Allow-Origin", "*"),
                    ("Access-Control-Allow-Methods", "GET, POST, PUT, DELETE, OPTIONS"),
                    ("Access-Control-Allow-Headers", "Content-Type, Authorization")
                ],
                body: BodyEmpty
             }
             Ok(resp)
        } else {
            match next(ctx) {
                Ok(res) -> {
                     respObj = autoResponse(res)
                     newHeaders = respObj.headers ++ [
                        ("Access-Control-Allow-Origin", "*"),
                        ("Access-Control-Allow-Methods", "GET, POST, PUT, DELETE, OPTIONS"),
                        ("Access-Control-Allow-Headers", "Content-Type, Authorization")
                     ]
                     resp: Response = { status: respObj.status, headers: newHeaders, body: respObj.body }
                     Ok(resp)
                }
                Fail(e) -> Fail(e)
            }
        }
    }
}

