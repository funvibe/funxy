package web

import "kit/ui" (VNode, render)
import "lib/json" (Json, jsonEncode, jsonFromValue)

// --- Response Helpers ---

fun response(status, body) -> Response {
    { status: status, headers: [], body: BodyString(body) }
}

fun resText(s) -> Response {
    { status: 200, headers: [("Content-Type", "text/plain")], body: BodyString(s) }
}

fun resHtml(node) -> Response {
    { status: 200, headers: [("Content-Type", "text/html")], body: BodyHtml(node) }
}

fun resJson(data) -> Response {
    { status: 200, headers: [("Content-Type", "application/json")], body: BodyJson(jsonFromValue(data)) }
}

fun redirect(url) -> Response {
    { status: 302, headers: [("Location", url)], body: BodyEmpty }
}

// --- Modifiers ---

fun withStatus(code, res: Response) -> Response {
    { status: code, headers: res.headers, body: res.body }
}

fun setHeader(key, val, res: Response) -> Response {
    newHeaders = res.headers ++ [(key, val)]
    { status: res.status, headers: newHeaders, body: res.body }
}

fun setCookie(name, val, res: Response) -> Response {
    cookieHeader = ("Set-Cookie", format("%s=%s; Path=/; HttpOnly", name, val))
    newHeaders = res.headers ++ [cookieHeader]
    { status: res.status, headers: newHeaders, body: res.body }
}

// --- Finalize ---

// Automatic response conversion (used internally by handleRequest)
fun autoResponse(val: HandlerResult) -> Response {
    match val {
        res: Response -> res
        s: String -> resText(s)
        node: VNode -> resHtml(node)
        j: Json -> resJson(j)
    }
}

fun finalizeResponse(res) -> HttpResponse {
    match res.body {
        BodyString(str) -> {
            { status: res.status, headers: res.headers, body: str }
        }
        BodyJson(j) -> {
            encoded = jsonEncode(j)
            { status: res.status, headers: res.headers, body: encoded }
        }
        BodyHtml(node) -> {
            rendered = render(node)
            { status: res.status, headers: res.headers, body: rendered }
        }
        BodyEmpty -> {
            { status: res.status, headers: res.headers, body: "" }
        }
    }
}
